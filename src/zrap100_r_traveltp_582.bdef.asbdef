managed implementation in class ZRAP100_BP_TRAVELTP_582 unique;
strict ( 2 );
with draft;
extensible;
define behavior for ZRAP100_R_TRAVELTP_582 alias Travel
persistent table zrap100_atrav582
extensible
draft table zrap100_dtrav582
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( global )
early numbering
{

  field ( readonly )
  CreatedBy,
  CreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( readonly )
  TravelId;

  field ( mandatory )
  CustomerID,
  BeginDate,
  EndDate;

  create;
  update ( features : instance );
  delete ( features : instance );

  determination setStatusToOpen on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateDates on save { create; field BeginDate, EndDate; }

  //  action deductDiscount result [1] $self;
  //  action deductDiscount parameter /dmo/a_travel_discount result [1] $self;
  action ( features : instance ) deductDiscount parameter /dmo/a_travel_discount result [1] $self;
  factory action copyTravel [1];

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  draft action Activate optimized;
  draft action Discard;
  draft action ( features : instance ) Edit;
  draft action Resume;

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDates; }

  mapping for zrap100_atrav582 corresponding extensible
    {
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      Attachment         = attachment;
      MimeType           = mime_type;
      FileName           = file_name;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}